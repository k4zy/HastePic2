apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

def env(String name, String defValue) {
    def value = System.getenv()[name]
    return value == null ? defValue : value
}

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.3"

    dataBinding {
        enabled = true
    }

    defaultConfig {
        applicationId "com.kazy.hastepic2"
        minSdkVersion 16
        targetSdkVersion 23
        versionCode 1
        versionName "1.0"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }

    signingConfigs {
        debug {
            storeFile = file('./debug_hastepic.keystore')
            storePassword = 'android'
            keyAlias = 'hastepic'
            keyPassword = 'android'
        }
        def releaseSettingGradleFile = new File(env("RELEASE_SETTING_GRADLE", "${project.rootDir}/hastepic.gradle"))
        if (releaseSettingGradleFile.exists()) {
            apply from: releaseSettingGradleFile, to: android
        } else {
            release {
                storeFile = debug.storeFile
                storePassword = debug.storePassword
                keyAlias = debug.keyAlias
                keyPassword = debug.keyPassword
            }
        }
    }
}

project.ext {
    supportLibraryVersion = '23.3.0'
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    testCompile 'junit:junit:4.12'
    compile "com.android.support:support-v4:${supportLibraryVersion}"
    compile "com.android.support:appcompat-v7:${supportLibraryVersion}"
    compile "com.android.support:recyclerview-v7:${supportLibraryVersion}"
    compile 'com.squareup.picasso:picasso:2.5.2'
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    kapt 'com.android.databinding:compiler:2.1.0'

    compile 'com.github.bumptech.glide:glide:3.7.0'

    compile 'io.reactivex:rxjava:1.1.3'
    compile('io.reactivex:rxandroid:1.1.0') {
        exclude group: 'io.reactivex', module: 'rxjava'
    }
}

kapt {
    generateStubs = true
}
